buildscript {
    ext.kotlin_version = '1.2.71'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'de.foam'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

repositories {
    mavenCentral()
}

dependencies {
    // Common dependencies
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // For Logging
    compile 'io.github.microutils:kotlin-logging:1.4.9'
    compile 'org.slf4j:slf4j-simple:1.7.25'

    // For argument parsing
    compile 'com.github.ajalt:clikt:1.3.0'

    // Variant "Upload to HDFS via CLI" - For object serialization to JSON
    compile 'com.google.code.gson:gson:2.8.2'

    // Variant "Upload to HBASE"
    // Keep in mind:
    // On HDP 2.6.5 and  HADOOP 2.7.3 HBASE 1.1.2 is used.
    // On local instance HBASE 1.1.2/2.1.0 and Hadoop 3.1.0 is used.
    // Both deployments work with following versions. But maybe in future the versions must be updated.
    // compile 'org.apache.hbase:hbase-client:1.4.3'
    compile 'org.apache.hbase:hbase-client:1.1.2'
    compile 'org.apache.hadoop:hadoop-client:3.0.0'

    //For Testing
    testCompile group: 'junit', name: 'junit', version: '4.12'

}

mainClassName = 'de.foam.dataimport.ForensicDataImporterKt'

// Following configuration is necessary to use slf4j-simple only.
// Because hbase-client wants to use slf4j-log4j12!
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'slf4j-log4j12') {
            details.useTarget "org.slf4j:log4j-over-slf4j:1.7.25"
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}