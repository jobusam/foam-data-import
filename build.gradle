buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        mavenCentral()

        // used for kirchmeier.capsule plugin
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // need kirchmeier.capsule plugin
        classpath "us.kirchmeier:gradle-capsule-plugin:1.0.2"
    }
}

group 'de.foam'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'

// us.kirchmeier.capsule plugin is used to build fat jars will all dependency for better deployment!
apply plugin: 'us.kirchmeier.capsule'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // Common dependencies
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // For Logging
    compile 'io.github.microutils:kotlin-logging:1.4.9'
    compile 'org.slf4j:slf4j-simple:1.7.25'

    // Variant "Upload to HDFS via CLI" - For object serialization to JSON
    compile 'com.google.code.gson:gson:2.8.2'

    // Variant "Upload to HBASE"
    compile 'org.apache.hbase:hbase-client:1.4.3'
    compile 'org.apache.hadoop:hadoop-client:3.0.0'

    //For Testing
    testCompile group: 'junit', name: 'junit', version: '4.12'

}

// Following configuration is necessary to use slf4j-simple only.
// Because hbase-client wants to use slf4j-log4j12!
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'slf4j-log4j12') {
            details.useTarget "org.slf4j:log4j-over-slf4j:1.7.25"
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task buildFatJar(type:FatCapsule){
    applicationClass 'de.foam.data.import.ForensicDataImporterKt'

    capsuleManifest {
        minJavaVersion = '1.8.0'
    }
}